// read the input file into the buffer
list[char] buffer
read_file("2025/04/input_files/input", buffer)
//read_file("2025/04/input_files/test1", buffer)

// print the content to console
println("stuff in the input file (size: {buffer.size()})")
for u64 index = 0; index < buffer.size(); index++:
    print(buffer.get(index))

// count the rows and columns, and create a list of values without newline
s16 rows = 0
s16 cols = 0
bool run = true
for u64 index = 0; run; index++:
    if buffer.get(index) == '\n':
        run = false
        cols = (s16)(index)
        rows = (s16)(buffer.size() / (index+1))
list[u8] rolls
for s16 row = 0; row < rows; row++:
    for s16 col = 0; col < cols; col++:
        rolls.add(buffer.get(row*(cols+1)+col) == '@')
println("rows: {rows}, cols: {cols}")

// print the input
for s16 row = 0; row < rows; row++:
    for s16 col = 0; col < cols; col++:
        print(rolls.get(row * cols + col))
    println("")

s16 check_roll(list[u8] rolls, s16 row, s16 col, s16 rows, s16 cols):
    if row < 0 || row >= rows || col < 0 || col >= cols:
        // outside means no roll there
        return 0
    else:
        // check if there is a roll there
        if (u8)rolls.get(row * cols + col):
            return 1
    return 0

// loop through the rolls to see which are accessible
u32 accessible_rolls = 0
bool rolls_removed = true
while rolls_removed:
    rolls_removed = false
    for s16 row = 0; row < rows; row++:
        for s16 col = 0; col < cols; col++:
            // only check if there is actually a roll here
            if rolls.get(row * cols + col):
                s16 num_rolls = 0
                // check all 8 adjecent positions if there is a roll there

                // check left top
                num_rolls += check_roll(rolls, row - 1, col - 1, rows, cols)

                // check top
                num_rolls += check_roll(rolls, row - 1, col, rows, cols)

                // check top right
                num_rolls += check_roll(rolls, row - 1, col + 1, rows, cols)

                // check left
                num_rolls += check_roll(rolls, row, col - 1, rows, cols)

                // check right
                num_rolls += check_roll(rolls, row, col + 1, rows, cols)

                // check left bottom
                num_rolls += check_roll(rolls, row + 1, col - 1, rows, cols)

                // check bottom
                num_rolls += check_roll(rolls, row + 1, col, rows, cols)

                // check bottom right
                num_rolls += check_roll(rolls, row + 1, col + 1, rows, cols)

                // check how many rolls we have
                if num_rolls < 4:
                    accessible_rolls += 1
                    // TODO: when this is the last line, and closing '"' is missing, results in Python IndexError
                    println("we can access the roll at {row}, {col} -> {accessible_rolls}")
                    rolls.set(row * cols + col, 0)
                    rolls_removed = 1

println("[solution] accessible rolls: {accessible_rolls}")
