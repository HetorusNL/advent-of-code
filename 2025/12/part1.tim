// read the input file into the buffer
list[char] buffer
read_file("2025/12/input_files/input", buffer)
//read_file("2025/12/input_files/test1", buffer)

// print the content to console
u64 buffer_size = buffer.size()
println("stuff in the input file (size: {buffer_size})")
for u64 index = 0; index < buffer_size; index++:
    print(buffer.get(index))

bool is_int(char c):
    return (bool)(c >= '0' && c <= '9')

u64 get_int(char c):
    return (u64)(c - '0')

// presents are stored in lists of 9 bools for every present
list[u1] presents

u64 index = 0
// first parse 6 presents
for u64 i = 0; i < 6; i++:
    // skip the 'x:\n'
    index = index + 3

    // parse 3 rows of 3 columns
    for u64 r = 0; r < 3; r++:
        for u64 c = 0; c < 3; c++:
            presents.add(buffer.get(index) == '#')
            index++
        // skip the '\n' after the row
        index++
    // skip the '\n' after the present
    index++

// regions and number of presents are stored in a list of 2 + 6 u64 values
list[u64] regions_num_presents

// then parse n lines of regions with number of presents
u64 value = 0
char c
while index < buffer.size():
    // parse numbers until a 'x'
    value = 0
    c = buffer.get(index)
    while is_int(c):
        value = value * 10
        value = value + get_int(c)
        index++
        c = buffer.get(index)
    index++  // skip the 'x'
    regions_num_presents.add(value)

    // parse numbers until a ':'
    value = 0
    c = buffer.get(index)
    while is_int(c):
        value = value * 10
        value = value + get_int(c)
        index++
        c = buffer.get(index)
    index = index + 2  // skip the ': '
    regions_num_presents.add(value)

    // parse the numbers of presents
    for u64 n = 0; n < 6; n++:
        value = 0
        c = buffer.get(index)
        while is_int(c):
            value = value * 10
            value = value + get_int(c)
            index++
            c = buffer.get(index)
        index++  // skip the space or newline
        regions_num_presents.add(value)

// print the presents
for u64 present = 0; present < 6; present++:
    println("present {present}:")
    for u64 pos = 0; pos < 9; pos ++:
        print("{presents.get(present * 9 + pos)}")
    println("")

// print the regions and number of presents
index = 0
while index < regions_num_presents.size():
    // print the grid
    print("{regions_num_presents.get(index)}x")
    index++
    print("{regions_num_presents.get(index)}: ")
    index++

    // print the number of presents
    for u64 n = 0; n < 6; n++:
        print("{regions_num_presents.get(index)} ")
        index++
    println("")

// get the area of each present
list[u64] present_areas
for u64 present = 0; present < 6; present++:
    u64 present_area = 0
    for u64 pos = 0; pos < 9; pos ++:
        present_area = present_area + (u64)presents.get(present * 9 + pos)
    println("present {present} area: {present_area}")
    present_areas.add(present_area)

// naively calculate if the present pixels fit in the area
u64 regions_that_fit = 0
for u64 line = 0; line < (regions_num_presents.size() / 8); line++:
    // get the area of the region
    u64 region_area = regions_num_presents.get(line * 8 + 0) * regions_num_presents.get(line * 8 + 1)
    println("region area: {region_area}")

    // calculate the area required for the presents
    u64 required_area = 0
    for u64 present = 0; present < 6; present++:
        u64 present_area = present_areas.get(present) * regions_num_presents.get(line * 8 + 2 + present)
        required_area = required_area + present_area
    println("required area: {required_area}")

    // if the region_area is large enough for the required_area, it fits!
    regions_that_fit = regions_that_fit + (u64)(region_area >= required_area)

println("[solution] the regions that fit all the listed resents are: {regions_that_fit}")
