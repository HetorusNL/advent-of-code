// read the input file into the buffer
list[char] buffer
read_file("2025/06/input_files/input", buffer)
//read_file("2025/06/input_files/test1", buffer)

// print the content to console
println("stuff in the input file (size: {buffer.size()})")
//for u64 index = 0; index < buffer.size(); index++:
//    print(buffer.get(index))

bool ignore_char(char c):
    return (bool)(c == ' ' || c == (char)0xa)

bool is_operator(char c):
    return (bool)(c == '*' || c == '+')  // '\n'  # TODO: fix that this returns bool

u64 get_int(char c):
    return (u64)(c - '0')

// calculate the line lengths and values per operation
u64 line_length = 0
bool run = true
for u64 index = 0; run; index++:
    line_length++
    if buffer.get(index) == (char)0xa:
        run = false
// values = total size - size of operators
u64 values_per_operation = (buffer.size() - line_length) / line_length
println("line_length: {line_length}, values_per_operation: {values_per_operation}")

// extract the operators
list[char] operators
u64 start_index = values_per_operation * line_length
for u64 index = start_index; index < buffer.size(); index++:
    char c = buffer.get(index)
    // if it's a space or newline, ignore
    if is_operator(c):
        // add the operator to the operators
        operators.add(c)
u64 num_operations = operators.size()
println("num operations: {num_operations}")

// scan the input and convert the columns to values
list[u64] values
list[u64] operation_sizes
u64 operation_size = 0
for u64 index = 0; index < line_length; index++:
    // start with a value of 0
    u64 value = 0

    // loop through all value lines
    for u64 value_line = 0; value_line < values_per_operation; value_line++:
        u64 char_index = value_line * line_length + index
        char c = buffer.get(char_index)
        if !ignore_char(c):
            value = value * 10
            value = value + get_int(c)

    // if we have a value, add it to the values list
    if value:
        values.add(value)
        operation_size++
    else:
        // if the value is 0, we finished a single operation
        operation_sizes.add(operation_size)
        operation_size = 0

println("num values: {values.size()}, num operation_sizes: {operation_sizes.size()}")

// perform the operations
list[u64] results
u64 index = 0
// loop through all operations
for u64 operation_index = 0; operation_index < num_operations; operation_index++:
    // create a single result with a default value
    u64 result = 0
    if operators.get(operation_index) == '*':
        result = 1  // for multiplication we need a start value of 1

    // extract the operation from the buffer
    char operation = operators.get(operation_index)

    // loop through all values per operator
    u64 operation_size = operation_sizes.get(operation_index)
    for u64 num_value = 0; num_value < operation_size; num_value++:
        // perform the operation
        u64 value = values.get(index)
        //print("{value} ")
        if operation == '+':
            result = result + value
        else:  // '*'
            result = result * value
        // increment the index
        index++

    // add the result
    results.add(result)
    //println("")

// calculate and print the results
// (actually the results list is in the reverse order, but we don't care :)
u64 result = 0
for u64 index = 0; index < results.size(); index++:
    u64 single_result = results.get(index)
    result = result + single_result
    //print("{single_result} ")
//println("")

println("[solution] the grand total to the individual problems is {result}")
